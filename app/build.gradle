repositories {
    mavenCentral()
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 35

    defaultConfig {
        namespace "io.heckel.ntfy"
        applicationId "io.heckel.ntfy"
        minSdk 34
        targetSdk 34

        versionCode 34
        versionName '1.18.0'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        /* Required for Room schema migrations */
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    flavorDimensions "store"
    productFlavors {
        play {
            buildConfigField 'boolean', 'FIREBASE_AVAILABLE', 'true'
            buildConfigField 'boolean', 'RATE_APP_AVAILABLE', 'true'
            buildConfigField 'boolean', 'INSTALL_PACKAGES_AVAILABLE', 'false'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        freeCompilerArgs += [
                '-Xjvm-default=all-compatibility' // https://stackoverflow.com/a/71234042/1440785
        ]
    }
    kotlin {
        jvmToolchain(17)
    }
    buildFeatures {
        buildConfig true
    }
}

// Disables GoogleServices tasks for F-Droid variant
android.applicationVariants.configureEach { variant ->
    def shouldProcessGoogleServices = variant.flavorName == "play"
    def googleTask = tasks.findByName("process${variant.name.capitalize()}GoogleServices")
    googleTask.enabled = shouldProcessGoogleServices
}

// Strips out REQUEST_INSTALL_PACKAGES permission for Google Play variant
android.applicationVariants.configureEach { variant ->
    def shouldStripInstallPermission = variant.flavorName == "play"
    if (shouldStripInstallPermission) {
        variant.outputs.each { output ->
            def processManifest = output.getProcessManifestProvider().get()
            processManifest.doLast { task ->
                def outputDir = task.getMultiApkManifestOutputDirectory().get().asFile
                def manifestOutFile = file("$outputDir/AndroidManifest.xml")
                def newFileContents = manifestOutFile.collect { s -> s.contains("android.permission.REQUEST_INSTALL_PACKAGES") ? "" : s }.join("\n")
                manifestOutFile.write(newFileContents, 'UTF-8')
            }
        }
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:2.0.21'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.0.21'

    // AndroidX, The Basics
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "androidx.core:core-ktx:1.15.0"
    implementation "androidx.constraintlayout:constraintlayout:2.2.0"
    implementation "androidx.activity:activity-ktx:1.9.3"
    implementation "androidx.fragment:fragment-ktx:1.8.5"
    implementation "androidx.work:work-runtime-ktx:2.10.0"
    implementation 'androidx.preference:preference-ktx:1.2.1'

    // JSON serialization
    implementation 'com.google.code.gson:gson:2.11.0'

    // Room (SQLite)
    def room_version = "2.6.1"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // OkHttp (HTTP library)
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    // Firebase, sigh ... (only Google Play)
    playImplementation 'com.google.firebase:firebase-messaging:24.0.3'

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:1.3.2"

    // Swipe down to refresh
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // Material design
    implementation "com.google.android.material:material:1.12.0"

    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.8.7"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Image viewer
    implementation 'com.github.stfalcon-studio:StfalconImageViewer:v1.0.1'
}
